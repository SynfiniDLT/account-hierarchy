-- Copyright (c) 2022 ASX Operations Pty Ltd. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Synfini.AccountHierarchy.Custody where

import DA.Finance.Types
import DA.Set (Set)
import qualified DA.Set as Set

-- | Agreement between beneficiary and custodian.
template CustodyAgreement
  with
    parent : Account -- ^ Account in which the custodian keeps assets of clients for safe-keeping.
    account : Account -- ^ Account owned by a beneficiary, which is allocated some portion of the assets in the
      -- parent account.
    observers : Set Party
  where
    signatory account.id.signatories, parent.id.signatories
    observer observers

    ensure account.provider == parent.owner

    key account.id : Id
    maintainer key.signatories

    -- | Update contract observers.
    choice CustodyAgreement_SetObservers : ContractId CustodyAgreement
      with
        newObservers : Set Party -- ^ Updated contract observers.
      controller account.owner
      do
        create this with observers = newObservers

-- | A request made to a custodian by a party seeking to act as a beneficiary of assets.
template CustodyRequest
  with
    parent : Account -- ^ Account in which the custodian keeps assets of clients for safe-keeping.
    account : Account -- ^ Account of prospective beneficiary.
  where
    signatory account.id.signatories
    observer parent.owner

    -- | Accept the request and create the 'CustodyAgreement'.
    choice CustodyRequest_Accept : ContractId CustodyAgreement
      controller parent.owner
      do
        exerciseByKey @CustodianRole parent.id CustodianRole_EnterCustodyAgreement with account

    -- | Cancel the request.
    choice CustodyRequest_Cancel : ()
      controller account.id.signatories
      do
        pure ()

    -- | Reject the request.
    choice CustodyRequest_Reject : ()
      controller parent.owner
      do
        pure ()

-- | Contract in which the account signatories allow the account owner to delegate beneficial ownership of assets in
-- the account to other parties. This contract also signifies that the account is not a leaf node in the tree.
template CustodianRole
  with
    parent : Account -- ^ Owner of this account can choose to enter 'CustodyAgreement' contracts (with this as the
    -- 'parent' account).
    observers : Set Party -- ^ Contract observers.
  where
    signatory parent.id.signatories
    observer parent.provider, parent.owner, observers

    key parent.id : Id
    maintainer key.signatories

    -- | Accept a request to act as a custodian.
    nonconsuming choice CustodianRole_EnterCustodyAgreement : ContractId CustodyAgreement
      with
        account : Account -- ^ Account of beneficiary.
      controller parent.owner, account.id.signatories
      do
        create CustodyAgreement with parent, account, observers = Set.empty
