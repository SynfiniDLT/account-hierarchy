-- Copyright (c) 2022 ASX Operations Pty Ltd. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Synfini.AccountHierarchy.Custody where

import DA.Assert ((===))
import DA.Finance.Types
import DA.Set (Set)
import qualified DA.Set as Set

-- | Agreement between beneficiary and custodian.
template CustodyAgreement
  with
    parent : Account -- ^ Account in which the custodian keeps assets of clients for safe-keeping.
    account : Account -- ^ Account owned by a beneficiary, which is allocated some portion of the assets in the
      -- parent account.
    observers : Set Party
  where
    signatory account.id.signatories, account.owner, parent.id.signatories
    observer observers

    ensure account.provider == parent.owner

    key account.id : Id
    maintainer key.signatories

    -- | Update contract observers.
    choice CustodyAgreement_SetObservers : ContractId CustodyAgreement
      with
        newObservers : Set Party -- ^ Updated contract observers.
      controller account.owner
      do
        create this with observers = newObservers

-- | A request made to a custodian by a party seeking to act as a beneficiary of assets.
template CustodyRequest
  with
    parent : Account -- ^ Account in which the custodian keeps assets of clients for safe-keeping.
    account : Account -- ^ Account of prospective beneficiary.
  where
    signatory account.owner
    observer parent.id.signatories, parent.provider, parent.owner, account.id.signatories, account.provider

    -- | Accept the request and create the 'CustodyAgreement'.
    choice CustodyRequest_Accept : ContractId CustodyAgreement
      controller account.id.signatories, parent.id.signatories
      do
        create CustodyAgreement with account, parent, observers = Set.empty

    -- | Cancel the request.
    choice CustodyRequest_Cancel : ()
      controller account.owner
      do
        pure ()

    -- | Reject the request.
    choice CustodyRequest_Reject : ()
      controller account.id.signatories, parent.id.signatories
      do
        pure ()

-- | Contract in which the account signatories allow the account owner to delegate beneficial ownership of assets in
-- the account to other parties.
template CustodianRole
  with
    account : Account -- ^ Owner of this account can choose to enter 'CustodyAgreement' contracts (with this as the
    -- 'parent' account).
    observers : Set Party -- ^ Contract observers.
  where
    signatory account.id.signatories
    observer account.provider, account.owner, observers

    key account.id : Id
    maintainer key.signatories

    -- | Accept a request to act as a custodian.
    nonconsuming choice CustodianRole_AcceptServiceRequest : ContractId CustodyAgreement
      with
        serviceRequestCid : ContractId CustodyRequest -- ^ Contract ID of the request.
      controller account.owner
      do
        serviceRequest <- fetch serviceRequestCid
        serviceRequest.parent === account
        exercise serviceRequestCid CustodyRequest_Accept

    -- | Reject a request to act as a custodian.
    nonconsuming choice CustodianRole_RejectServiceRequest : ()
      with
        serviceRequestCid : ContractId CustodyRequest -- ^ Contract ID of the request.
      controller account.owner
      do
        serviceRequest <- fetch serviceRequestCid
        serviceRequest.parent === account
        exercise serviceRequestCid CustodyRequest_Reject
